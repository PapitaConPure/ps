LEER Número base con 12
LEER Número balas con 7
LEER Número relojes con 5
LEER Número palancas con 6
LEER Número cristales con 4

SI base precede 0
o balas precede 0
o relojes precede 0
o palancas precede 0
o cristales precede 0
    ENVIAR "😡"
    TERMINAR
FIN

CARGAR pcs con Función(n)
  CARGAR n con n * 1000
  DEVOLVER n->redondear() / 1000
FIN

CREAR Marco m
USAR m->asignarColor(colorDiscord)
USAR m->asignarTítulo("Tabla de daños")

CARGAR d con Registro
  bl: 1 + .2 * balas,
  rl: 1 + .2 * relojes,
  pl: 1 + .75 * palancas,
  ct: 1 + .2 * cristales,
  cr: 2,

CARGAR casos con Lista
  d->rl,
  d->rl * d->bl * d->pl * d->ct * d->cr,

  d->rl * d->bl,
  d->rl * d->pl,
  d->rl * d->ct,

  d->rl * d->cr,

  d->rl * d->bl * d->cr,
  d->rl * d->pl * d->cr,
  d->rl * d->ct * d->cr,

  d->rl * d->bl * d->pl * d->cr,
  d->rl * d->bl * d->ct * d->cr,

PARA i con 0 MIENTRAS i precede casos->largo SUMAR i
  CARGAR mult con casos->(i)
  CARGAR casos->(i) con Texto pcs(base * mult)
FIN

EJECUTAR m->agregarCampo("Base", (
"💥 ", base,
" 💊 x ", balas,
" ⌚ x ", relojes,
" 🏑 x ", palancas,
" 💎 x ", cristales
), Falso)

EJECUTAR m
  ->agregarCampo("Crítico", casos->5, Verdadero)
  ->agregarCampo("Peor", casos->0, Verdadero)
  ->agregarCampo("Mejor", casos->1, Verdadero)

  ->agregarCampo("Vs. jefe", casos->2, Verdadero)
  ->agregarCampo(">90% HP", casos->3, Verdadero)
  ->agregarCampo("De cerca", casos->4, Verdadero)

  ->agregarCampo("...jefe", casos->6, Verdadero)
  ->agregarCampo("...>90% HP", casos->7, Verdadero)
  ->agregarCampo("...cerca", casos->8, Verdadero)

  ->agregarCampo("...jefe >90% HP", casos->9, Verdadero)
  ->agregarCampo("...jefe cerca", casos->10, Verdadero)

ENVIAR m
